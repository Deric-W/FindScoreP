# has to run in project mode to register targets
cmake_minimum_required(VERSION 3.20)
project(
    TestDeterminedInstrumentations
    DESCRIPTION "Test determined instrumentations"
    LANGUAGES C
)

add_library(A STATIC empty.c)
add_library(B SHARED empty.c)
add_library(C STATIC empty.c)
add_library(D STATIC empty.c)
add_library(E STATIC empty.c)
add_library(F STATIC empty.c)
add_library(G STATIC empty.c)
add_executable(main1 empty.c)
add_executable(main2 empty.c)
add_executable(main3 empty.c)
add_executable(main4 empty.c)
add_library(lib1 SHARED empty.c)

target_link_libraries(main1 PUBLIC A F G)
target_link_libraries(main1 PRIVATE B)
target_link_libraries(main1 INTERFACE C)
target_link_libraries(main2 PRIVATE G)
target_link_libraries(main3 PRIVATE lib1)
target_link_libraries(main4 PRIVATE lib1)
target_link_libraries(lib1 PRIVATE A)
target_link_libraries(B PRIVATE D)
target_link_libraries(B INTERFACE E B)

include("${CMAKE_CURRENT_LIST_DIR}/../utils.cmake")
include(ScorePUtilities)

function(_check_target_languages target languages)
    get_target_property(targetLanguages "${target}" SCOREP_LANGUAGES)
    if(targetLanguages STREQUAL "targetLanguages-NOTFOUND" AND NOT languages STREQUAL "NOTFOUND")
        message(FATAL_ERROR "target '${target}' has no Score-P languages configured")
    elseif((NOT targetLanguages STREQUAL "targetLanguages-NOTFOUND") AND languages STREQUAL "NOTFOUND")
        message(FATAL_ERROR "target '${target}' has Score-P languages '${targetLanguages}' configured")
    elseif(NOT (targetLanguages STREQUAL "targetLanguages-NOTFOUND" OR languages STREQUAL "NOTFOUND"))
        _check_lists_equal("${targetLanguages}" "${languages}")
    endif()
endfunction()

function(_check_target_arguments target language arguments)
    get_target_property(targetArguments "${target}" "SCOREP_${language}_ARGUMENTS")
    if(targetArguments STREQUAL "targetArguments-NOTFOUND" AND NOT arguments STREQUAL "NOTFOUND")
        message(FATAL_ERROR "target '${target}' has no Score-P arguments configured")
    elseif((NOT targetArguments STREQUAL "targetArguments-NOTFOUND") AND arguments STREQUAL "NOTFOUND")
        message(FATAL_ERROR "target '${target}' has Score-P arguments '${targetArguments}' configured")
    elseif(NOT (targetArguments STREQUAL "targetArguments-NOTFOUND" OR arguments STREQUAL "NOTFOUND"))
        _check_lists_equal("${targetArguments}" "${arguments}")
    endif()
endfunction()

scorep_mark(INSTRUMENT main1 LANGS C ARGUMENTS "--compiler")
scorep_mark(INSTRUMENT main4 LANGS C)
scorep_mark(INSTRUMENT E LANGS C CXX ARGUMENTS "--user")
scorep_mark(INSTRUMENT A LANGS C ARGUMENTS "--mpp=mpi")
scorep_mark(HINT G LANGS C ARGUMENTS "--pdt")
scorep_mark(HINT D LANGS C ARGUMENTS "--thread=omp" "--nocompiler")

scorep_determine_instrumentations("A;B;C;D;E;F;G;main1;main2;main3;main4;lib1")

_check_target_languages(A "C")
_check_target_languages(B "NOTFOUND")
_check_target_languages(C "NOTFOUND")
_check_target_languages(D "NOTFOUND")
_check_target_languages(E "C;CXX")
_check_target_languages(F "NOTFOUND")
_check_target_languages(G "NOTFOUND")
_check_target_languages(main1 "C;CXX")
_check_target_languages(main2 "NOTFOUND")
_check_target_languages(main3 "NOTFOUND")
_check_target_languages(main4 "C")
_check_target_languages(lib1 "C")

_check_target_arguments(A C "--thread=omp;--mpp=mpi;--user;--compiler;--pdt")
_check_target_arguments(E C "--thread=omp;--mpp=mpi;--user;--compiler;--pdt")
_check_target_arguments(E CXX "--thread=omp;--mpp=mpi;--user;--compiler;--pdt")
_check_target_arguments(main1 C "--thread=omp;--mpp=mpi;--user;--compiler;--pdt")
_check_target_arguments(main1 CXX "--thread=omp;--mpp=mpi;--user;--compiler;--pdt")
_check_target_arguments(main4 C "--thread=omp;--mpp=mpi")
_check_target_arguments(lib1 C "--thread=omp;--mpp=mpi;--user;--compiler;--pdt")
